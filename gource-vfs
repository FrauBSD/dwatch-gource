# -*- tab-width: 4 -*- ;; Emacs
# vi: set filetype=sh tabstop=8 shiftwidth=8 noexpandtab :: Vi/ViM
############################################################ IDENT(1)
#
# $Title: dwatch(8) gource module for VOP_*(9) activity $
# $Copyright: 2014-2018 Devin Teske. All rights reserved. $
# $FrauBSD: dwatch-gource/gource-vfs 2018-05-12 04:56:00 +0000 freebsdfrau $
#
############################################################ DESCRIPTION
#
# Produce gource custom log format for filesystem activity
#
############################################################ PROBE

load_profile gource-vfs-raw

############################################################ GLOBALS

: ${_DEBUG=}

############################################################ MAIN

[ "$DEBUG$EXIT_AFTER_COMPILE" ] || info "Watching '$PROBE' ..."
eval dwatch $ARGV -qFX gource-vfs-raw | awk -v debug=$_DEBUG '
	BEGIN {
		date  = "[[:digit:]]+ [A-Z][a-z][a-z] [0-9 ][0-9]"
		time  = "[0-9][0-9]:[0-9][0-9]:[0-9][0-9]"
	}
	debug { printf "DEBUG: %s\n", $0 > "/dev/stderr" }
	length(datetime = substr($0, 1, 20)) != 20 { next }
	datetime !~ "^" date " " time "$" { next }
	(epoch = $9) !~ /^[[:digit:]]+/ { next }
	(execname = $6) && sub(/\[[[:digit:]]+\]:$/, "", execname) {
		if ((probe = $8) ~ /remove|rm/) type = "D"
		else if (probe ~ /lookup/) type = "M"
		else type = "A"
		sub(/.*:entry [[:digit:]]+ /, "")
		printf "%u|%s|%s|%s\n", epoch, execname, type, (path = $0)
	}
' # END-QUOTE

exit $SUCCESS

################################################################################
# END
################################################################################
